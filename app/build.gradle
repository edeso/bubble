apply plugin: 'com.android.application'

def getVersionCode = { ->
    def code = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'tag', '--list'
        standardOutput = code
    }
    return code.toString().split("\n").size()
}


def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine "git", "describe", "--tags", "--long"
        standardOutput = stdout
    }
    return stdout.toString('UTF-8').trim() //.replaceAll("[^\\x00-\\x7F]", "")
}

repositories {
    mavenCentral()
    mavenLocal()
    google()
    // jp2-android repo
    maven {
        url "https://jcenter.bintray.com/"
    }
    // commons-compress rc-1 location
    maven {
        url "https://repository.apache.org/content/repositories/orgapachecommons-1628/"
    }
}

android {
    compileSdk 33
    //buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "com.github.edeso.bubble2"
        namespace "com.nkanaev.comics"

        minSdk 16
        targetSdk 33
        multiDexEnabled true

        versionName getVersionName()
        versionCode getVersionCode()
        setProperty("archivesBaseName", applicationId + "-v" + versionCode + "(" + versionName + ")")

        manifestPlaceholders = [
                appIcon: "@drawable/icon_squared"
        ]
   }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            manifestPlaceholders = [
                    appIcon: "@drawable/icon_squared_debug"
            ]
        }
        release {
            // can't use either of the below, f**ks up JP2 support

            // Enables code shrinking, obfuscation, and optimization for only
            // your project's release build type.
            minifyEnabled false
            // Enables resource shrinking, which is performed by the
            // Android Gradle plugin.
            shrinkResources false
        }
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES', 'META-INF/DEPENDENCIES.txt', 'META-INF/LICENSE', 'META-INF/LICENSE.txt', 'META-INF/NOTICE', 'META-INF/NOTICE.txt']
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    // for multiple dex files on pre-Android 5 devices
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.startup:startup-runtime:1.1.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.0'
    implementation 'androidx.fragment:fragment:1.5.7'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.0-alpha09'
    implementation 'com.github.junrar:junrar:7.5.3'
    implementation 'com.squareup.picasso:picasso:2.8'
    implementation 'org.apache.commons:commons-compress:1.23.0'
    //implementation fileTree(dir: 'lib', include: ['*.jar'])
    implementation 'org.tukaani:xz:1.9'
    implementation 'com.github.luben:zstd-jni:1.5.2-5@aar'
    implementation 'org.brotli:dec:0.1.2'
    implementation 'com.gemalto.jp2:jp2-android:1.0.3'
}
